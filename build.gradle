plugins {
    id 'application'
    id 'java'
    id 'antlr'
    id 'idea'
}

group = 'net.nlacombe'
version = '2.0.0'

def junitVersion = '5.7.0'

dependencies {
    antlr 'org.antlr:antlr4:4.9.1'
    implementation 'org.antlr:stringtemplate:4.0.2'
    implementation 'com.yuvalshavit:antlr-denter:1.1'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

repositories {
    mavenCentral()
    mavenLocal()
}

generateGrammarSource {
    arguments += ["-visitor", "-package", "net.nlacombe.prophecy.parser.antlr4"]
}

application {
    mainClass = 'net.nlacombe.prophecy.ProphecyMain'
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '7.0.2'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}
